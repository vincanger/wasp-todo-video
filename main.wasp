app TodoVideo {
  wasp: {
    version: "^0.7.3"
  },
  title: "ToDo",

  auth: {
    userEntity: User,
    methods: {
      usernameAndPassword: {}, 
    },
    onAuthFailedRedirectTo: "/login",
  },

  dependencies: [
    ("nodemailer", "6.8.0")
  ]
}

// Use Prisma Schema Language (PSL) to define our entities: https://www.prisma.io/docs/concepts/components/prisma-schema
// Run `wasp db migrate-dev` in the CLI to create the database tables
// Then run `wasp db studio` to open Prisma Studio and view your db models
entity User {=psl 
  id          Int     @id @default(autoincrement())
  username    String  @unique
  password    String  
  tasks       Task[]
psl=}

entity Task {=psl
    id          Int     @id @default(autoincrement())
    description String
    isDone      Boolean @default(false)
    user        User?   @relation(fields: [userId], references: [id])
    userId      Int?
psl=}

route RootRoute { path: "/", to: MainPage }
page MainPage {
  authRequired: true,
  component: import Main from "@client/MainPage"
}

route LoginRoute { path: "/login", to: LoginPage }
page LoginPage {
  component: import Login from "@client/LoginPage"
}

route SignupRoute { path: "/signup", to: SignupPage }
page SignupPage {
  component: import Signup from "@client/SignupPage"
}

query getTasks {
  fn: import { getTasks } from "@server/queries.js",
  entities: [Task]
}

action createTask {
  fn: import { createTask } from "@server/actions.js",
  entities: [Task]
}

action updateTask {
  fn: import { updateTask } from "@server/actions.js",
  entities: [Task]
}

// job sendEmail {
//   executor: PgBoss,
//   perform: {
//     fn: import { emailSender } from "@server/workers/emailSender.js"
//   },
//   schedule: {
//     cron: "* * * * *"
//   },
//   entities: [Task]
// }